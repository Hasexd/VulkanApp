#version 450
layout(local_size_x = 16, local_size_y = 16) in;
layout(binding = 0) uniform sampler2D srcMip;
layout(binding = 1, rgba16f) uniform image2D dstMip;

vec4 UpsampleTent(sampler2D tex, vec2 uv, vec2 texelSize) {
    vec4 d = texelSize.xyxy * vec4(1.0, 1.0, -1.0, 0.0);
    
    vec4 s;
    s  = texture(tex, uv - d.xy);
    s += texture(tex, uv - d.wy) * 2.0;
    s += texture(tex, uv - d.zy);
    
    s += texture(tex, uv + d.zw) * 2.0;
    s += texture(tex, uv       ) * 4.0;
    s += texture(tex, uv + d.xw) * 2.0;
    
    s += texture(tex, uv + d.zy);
    s += texture(tex, uv + d.wy) * 2.0;
    s += texture(tex, uv + d.xy);
    
    return s * (1.0 / 16.0);
}

void main() {
    ivec2 dstPos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 dstSize = imageSize(dstMip);
    
    if (dstPos.x >= dstSize.x || dstPos.y >= dstSize.y) {
        return;
    }
    
    vec2 texCoord = (vec2(dstPos) + 0.5) / vec2(dstSize);
    vec2 texelSize = 1.0 / vec2(textureSize(srcMip, 0));
    
    vec4 upsampledColor = UpsampleTent(srcMip, texCoord, texelSize);
    vec4 existingColor = imageLoad(dstMip, dstPos);
    
    vec4 finalColor = existingColor + upsampledColor * 0.65;
    
    imageStore(dstMip, dstPos, finalColor);
}