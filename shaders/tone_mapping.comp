#version 450

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(binding = 0, rgba16f) readonly uniform image2D HDRImage;
layout(binding = 1, rgba8) writeonly uniform image2D LDRImage;

vec3 RttAndOdtFit(vec3 v)
{
    vec3 a = v * (v + 0.0245786) - 0.000090537;
    vec3 b = v * (0.983729 * v + 0.4329510) + 0.238081;
    return a / b;
}

void main()
{
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    vec2 imageSize = vec2(imageSize(LDRImage));
    vec2 coord = (vec2(pixelCoord) / imageSize) * 2.0 - 1.0;
    coord.y = -coord.y;

    mat3 acesInputMat = mat3(
        0.59719, 0.07600, 0.02840,
        0.35458, 0.90834, 0.13383,
        0.04823, 0.01566, 0.83777
    );

    mat3 acesOutputMat = mat3(
         1.60475, -0.10208,  0.00327,
        -0.53108,  1.10813, -0.07276,
        -0.07367, -0.00605,  1.07602
    );

    vec3 color = vec3(imageLoad(HDRImage, pixelCoord));
    float exposure = 1.5;

    color *= exposure;
    color = acesInputMat * color;
    color = RttAndOdtFit(color);
    color = acesOutputMat * color;
    color = pow(color, vec3(1.0/2.2));
    color = clamp(color, 0.0, 1.0);

	imageStore(LDRImage, pixelCoord, vec4(color, 1.0));
}